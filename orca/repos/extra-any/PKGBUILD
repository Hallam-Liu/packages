# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=3.28.0
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
url="https://wiki.gnome.org/Projects/Orca"
arch=(any)
license=(LGPL)
depends=(gtk3 at-spi2-atk python-atspi python-dbus python-xdg speech-dispatcher liblouis brltty
         xorg-xmodmap gsettings-desktop-schemas
         gst-plugins-base # playbin, audiotestsrc, basic decoders
         gst-plugins-good # pulsesink, more decoders
)
makedepends=(itstool intltool git yelp-tools)
groups=(gnome)
_commit=043487f8ccd75a702fb2e6fe08799393b3f1bd0e  # tags/ORCA_3_28_0^0
source=("git+https://git.gnome.org/browse/orca#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/ORCA_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
