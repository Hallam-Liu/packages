# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=srt
pkgver=1.2.3
pkgrel=1
pkgdesc="Secure Reliable Transport library"
url="https://www.srtalliance.org/"
arch=(x86_64)
license=(MPL2)
depends=(openssl)
makedepends=(git cmake ninja)
_commit=1367914ea5d7e26be8274e2fffe8b3e7b05ad193  # tags/v1.2.3
source=("git+https://github.com/Haivision/srt#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  mkdir build
}

build() {
  cd build
  cmake -G Ninja ../srt \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

check() {
  cd build
  ./utility-test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
# vim:set ts=2 sw=2:


