# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=xinetd-runit
pkgver=20180224
pkgrel=1
pkgdesc="runit service scripts for xinetd"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
groups=('runit-system')
depends=('xinetd')
provides=('xinetd-runscripts')
replaces=('xinetd-runscripts')
conflicts=('fcron' 'fcron-runit' 'systemd-sysvcompat')
source=("xinetd.run::${_url}/xinetd/files/xinetd/run")
sha256sums=('abade658564cfb5f7ed9d343be80a33195df632e82577a6574b45804ab9b7b5b')

_inst_sv(){
    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'xinetd'
}
