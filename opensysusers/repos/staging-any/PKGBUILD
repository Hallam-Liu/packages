# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Maintainer: artoo <artoo@artixlinux.org>

pkgname=opensysusers
pkgver=0.4.1
pkgrel=1
pkgdesc="A standalone utility for handling systemd-style sysusers.d users and groups"
arch=('any')
url="https://github.com/artix-linux/opensysusers"
license=('BSD2')
depends=('pacman')
makedepends=('docbook-xsl')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/artix-linux/opensysusers/archive/${pkgver}.tar.gz"
		'opensysusers.hook'
		'sysusers-hook')
sha256sums=('e3b9d415779987b103ff2e9a79e2c3b2e3497296c25a4e757c7098ae4febf093'
            'e07d6258b37764190e4ab0f64d08ad9688b3a34b6c100be2d4aba970a8e26aee'
            'd0add2ecae5619113640a454bd545e7317e3e7f72702b3f8b995322147fd2b16')

prepare(){
    cd "${srcdir}/${pkgname}-${pkgver}"
}

build(){
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PREFIX=/usr DESTDIR="${pkgdir}"
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install

    install -vDm0755 openrc/opensysusers.initd "${pkgdir}"/etc/init.d/opensysusers
    install -dm0755 "${pkgdir}/etc/runlevels/boot"
    ln -snf "/etc/init.d/opensysusers" "${pkgdir}/etc/runlevels/boot/opensysusers"

    # pacman hooks
    install -Dm755 ${srcdir}/sysusers-hook "$pkgdir"/usr/share/libalpm/scripts/sysusers-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook
}
