# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Maintainer: artoo <artoo@artixlinux.org>

pkgname=opensysusers
pkgver=0.3.2
pkgrel=5
pkgdesc="A standalone utility for handling systemd-style sysusers.d users and groups"
arch=('any')
url="https://github.com/artix-linux/opensysusers"
license=('BSD2')
depends=('pacman')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/artix-linux/opensysusers/archive/${pkgver}.tar.gz"
		'opensysusers.hook'
		'sysusers-hook'
		sysgid.patch::"https://github.com/artix-linux/opensysusers/commit/3f451ccfe5e5943d0ebc98f5f0d095a6c0ee11b4.patch")
sha256sums=('0a3ffb2bf5a3baa9bd49ef522d4bef7fbfa2f51ca6cce24d6354a7ae3c0344d8'
            'e07d6258b37764190e4ab0f64d08ad9688b3a34b6c100be2d4aba970a8e26aee'
            '086f951d1ebb0c7344245bd4dbdd7459bd34f5f2b88784a35e4ed2627a6f57f0'
            '8575316ee9ab0f9b751e7856fbe28bfbe3d7442d56eab7582342d7bd8f1566d9')

prepare(){
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np 1 -i ${srcdir}/sysgid.patch
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make bindir="/usr/bin" DESTDIR="${pkgdir}" install

    ln -snf "/usr/bin/sysusers" "${pkgdir}/usr/bin/systemd-sysusers"

    install -vDm0755 openrc/opensysusers.initd "${pkgdir}"/etc/init.d/opensysusers
    install -dm0755 "${pkgdir}/etc/runlevels/boot"
    ln -snf "/etc/init.d/opensysusers" "${pkgdir}/etc/runlevels/boot/opensysusers"

    # pacman hooks
    install -Dm755 ${srcdir}/sysusers-hook "$pkgdir"/usr/share/libalpm/scripts/sysusers-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook
}
