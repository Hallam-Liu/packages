# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=eglexternalplatform
pkgver=0+3
pkgrel=2
pkgdesc="EGL External Platform interface"
url=https://github.com/NVIDIA/eglexternalplatform
arch=(any)
license=(MIT)
depends=(mesa)
makedepends=(git)
_commit=e3b182e3253f92bdbb03a71fdbd958bfb69cf3e3  # master
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  printf '0+%s' "$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/include/EGL" -m644 interface/*
  install -Dt "$pkgdir/usr/share/pkgconfig" -m644 *.pc
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 et:
