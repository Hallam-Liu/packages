# Maintainer: artoo <artoo@cromnix.org>

pkgbase=artools
pkgname=('artools-base' 'artools-pkg' 'artools-iso-base' 'artools-iso')
pkgver=0.7.6
pkgrel=2
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://github.com/artix-linux/artools'
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
        find-repo.patch::https://github.com/artix-linux/artools/commit/d6bf8bcf12d9a891a7b6f5c1e64e18e549958261.patch)
sha256sums=('f3045c852ddb7c7e0588fdb7e6cd7c8ef3e779c69035afa5798eb8dcc4e3b3c0'
            'ecf9e83df38c96ccff20a431344389c9660774b34fb90d0a2331bd132d0c856c')

prepare() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    sed -e "s/^Version=.*/Version=${pkgver}/" -i Makefile
    patch -Np 1 -i ${srcdir}/find-repo.patch
}

build() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools')
    backup=('etc/artools/artools.conf')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'git')
    conflicts=('devtools')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-iso-base() {
    pkgdesc='Development tools for Artix (ISO base profile)'
    depends=('artools-base')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_isobase
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('artools-iso-base' 'dosfstools' 'libisoburn'
            'squashfs-tools' 'mkinitcpio' 'mktorrent' 'grub')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

