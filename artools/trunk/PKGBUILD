# Maintainer: artoo <artoo@cromnix.org>

pkgbase=artools
pkgname=('artools-base' 'artools-pkg' 'artools-iso')
pkgver=0.8.5
pkgrel=2
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://github.com/artix-linux/artools'
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
        arch.patch::https://github.com/artix-linux/artools/commit/50f90ab53cd926b8dbf397c58f3241dc27a29544.patch)
sha256sums=('c426927138b2db1db04251d872fe7ef83c5407d5040d36645d63953a570a542c'
            'd1340e3909d073339b4bfd366569460a6e32ac82fee9a58f976fd136069cf1bf')

prepare() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    sed -e "s/^VERSION=.*/VERSION=${pkgver}/" -i Makefile
    patch -Np 1 -i ${srcdir}/arch.patch
}

build() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    make PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools')
    backup=('etc/artools/artools.conf')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'git')
    conflicts=('devtools')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('dosfstools' 'libisoburn' 'iso-profiles'
            'squashfs-tools' 'mkinitcpio' 'mktorrent' 'grub')
    conflicts=('artools-iso-base')
    replaces=('artools-iso-base')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

