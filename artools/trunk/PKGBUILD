# Maintainer: artoo <artoo@cromnix.org>

pkgbase=artools
pkgname=('artools-base' 'artools-pkg' 'artools-iso-base' 'artools-iso')
pkgver=0.7.6.1
pkgrel=1
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://github.com/artix-linux/artools'
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha256sums=('0c19d7d13ba4ca9d28aff8d89c45bd873626f027f8cb14ad77c5af07179c63f7')

prepare() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    sed -e "s/^Version=.*/Version=${pkgver}/" -i Makefile
}

build() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools')
    backup=('etc/artools/artools.conf')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'git')
    conflicts=('devtools')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-iso-base() {
    pkgdesc='Development tools for Artix (ISO base profile)'
    depends=('artools-base')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_isobase
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('artools-iso-base' 'dosfstools' 'libisoburn'
            'squashfs-tools' 'mkinitcpio' 'mktorrent' 'grub')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

