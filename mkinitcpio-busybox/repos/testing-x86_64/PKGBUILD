# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas Bächler <thomas@archlinux.org>

pkgname=mkinitcpio-busybox
pkgver=1.28.4
pkgrel=1
pkgdesc='Base initramfs tools'
arch=(x86_64)
url="http://www.busybox.net/"
license=(GPL)
depends=(glibc)
options=(!buildflags)
source=(https://busybox.net/downloads/busybox-$pkgver.tar.bz2
        config)
sha256sums=('e3c14a3699dc7e82fed397392957afc78e37bdf25398ac38ead6e84621b2ae6a'
            'cb97bf879450457c97cb2a2b6c0e4821ce6fc8962cca36e4039b3d7e8498bf38')

prepare() {
  cd "busybox-$pkgver"

  local safeflags="-march=${CARCH/_/-} -mtune=generic -Os -pipe -fno-strict-aliasing"

  sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="'"$safeflags"'"|' "$srcdir/config" > .config
}

build() {
  make -C "busybox-$pkgver"
}

package() {
  install -Dm755 "$srcdir/busybox-$pkgver/busybox" "$pkgdir/usr/lib/initcpio/busybox"
}
