# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=pinentry
pkgver=1.1.0
pkgrel=2
pkgdesc='Collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol'
url='http://gnupg.org/related_software/pinentry/'
arch=('x86_64')
license=('GPL')
depends=('ncurses' 'libcap' 'libassuan' 'libsecret')
makedepends=('gtk2' 'qt5-base' 'gcr')
optdepends=('gtk2: gtk2 backend'
            'qt5-base: qt backend'
            'gcr: gnome3 backend')
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        'pinentry')
sha256sums=('68076686fa724a290ea49cdf0d1c0c1500907d1b759a3bcbfbec0293e8f56570'
            'SKIP'
            '50d651d7379dc160bf08a1f888e364fb366e2658c0a709f78a3d7965c3f2224d')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--enable-pinentry-tty \
		--enable-pinentry-curses \
		--enable-fallback-curses \
		--enable-pinentry-emacs \
		--enable-pinentry-gtk2 \
		--enable-pinentry-gnome3 \
		--enable-pinentry-qt \
		--enable-libsecret \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dt "${pkgdir}/usr/bin" ../pinentry
}
