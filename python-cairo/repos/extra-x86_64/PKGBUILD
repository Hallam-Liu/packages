# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>  
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=python-cairo
pkgname=(python-cairo python2-cairo)
pkgver=1.16.2
pkgrel=1
pkgdesc="Python bindings for the cairo graphics library"
arch=('x86_64')
url="https://pycairo.readthedocs.io/en/latest/"
license=('LGPL2.1' 'MPL')
makedepends=(python python2 cairo git)
_commit=c74dc1e3589bd8a197582836c7db2ab400a988bb  # tags/v1.16.2
source=("git+https://github.com/pygobject/pycairo/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pycairo
  git describe --tags | sed 's/^v//;s/-/+/g'
}


build() {
  cd pycairo
  python2 setup.py build
  python setup.py build
}

package_python2-cairo() {
  depends=('cairo' 'python2')
  pkgdesc="Python2 bindings for the cairo graphics library"

  cd pycairo
  python2 setup.py install --skip-build --root="${pkgdir}" --optimize='1'
}

package_python-cairo() {
  depends=('cairo' 'python')

  cd pycairo
  python setup.py install --skip-build --root="${pkgdir}" --optimize='1'
}
