# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xorg-xkbcomp
pkgver=1.4.1
pkgrel=2
pkgdesc="X Keyboard description compiler"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxkbfile')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(${url}/releases/individual/app/xkbcomp-${pkgver}.tar.bz2{,.sig}
        0001-Ignore-xkb_keycodes.maximum-of-255.patch)
sha512sums=('443397e112f67e2ebc5150ec0dbf0f4a072ea39585d964d2c7cbd0569b7e0ce7d8bed0bc2c2d886343fdb060c941e65b42aac16adf189c66e90849029e278f23'
            'SKIP'
            '4005d187a3d585fd4165254c2a4a17cd381d9cf04dfef649de47ecb26dfbe1442e42b1c87e4aa9a3d7f45054393a5371299167a8c089d50e981a74b764e7353c')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>
validpgpkeys+=('A66D805F7C9329B4C5D82767CCC4F07FAC641EFF') # "Daniel Stone <daniels@collabora.com>"
validpgpkeys+=('DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3') # "Adam Jackson <ajax@benzedrine.nwnk.net>"

prepare() {
  cd xkbcomp-${pkgver}
  # FS58908; https://bugzilla.redhat.com/show_bug.cgi?id=1587998
  patch -Np1 -i ../0001-Ignore-xkb_keycodes.maximum-of-255.patch
}

build() {
  cd xkbcomp-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xkbcomp-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
