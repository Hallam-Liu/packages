# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=2.26.1+2+g493b5a1
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
url="https://wiki.gnome.org/Accessibility"
arch=(x86_64)
license=(GPL2)
depends=(at-spi2-core libx11 libxml2 atk)
makedepends=(git meson)
_commit=493b5a1c83b248f382b677a4f1fbfb7e31f0dc4f  # master
source=("git+https://git.gnome.org/browse/at-spi2-atk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^AT_SPI2_ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
