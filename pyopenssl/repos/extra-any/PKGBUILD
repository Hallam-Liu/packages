# $Id$
# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=pyopenssl
pkgname=('python-pyopenssl' 'python2-pyopenssl')
pkgver=17.5.0
pkgrel=2
arch=('any')
pkgdesc="Python3 wrapper module around the OpenSSL library"
url='https://github.com/pyca/pyopenssl'
license=('Apache')
makedepends=('openssl' 'python-setuptools' 'python2-setuptools' 'python-cryptography'
             'python2-cryptography' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pretend' 'python2-pretend'
              'python-flaky' 'python2-flaky')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/pyca/pyopenssl/archive/${pkgver}.tar.gz
        ${pkgname}-17.5.0-fix-import.patch::https://github.com/pyca/pyopenssl/commit/993c4e4afc4274019bdb835b64191afeed6c13b7.patch)
sha512sums=('de297ed6b38a0b0cb6f9aca1c916fe8daacb8ba88d68998a436082789700c8a2d0c470ee8836d8eb43243104937fa310a41dbaa21a3142a65c347e9430f6bada'
            'c1cb7cce6c31d1b41af515d4878275604ba4d99068e13a10caad23acde7c9e2c6de10bdc533c30c55bb83c9c66b10599b8a2b1fbe83d492bd686fd7bf0a57489')

prepare() {
  sed 's|,<3.3.0||' -i pyopenssl-$pkgver/setup.py
  patch -Np1 -d pyopenssl-$pkgver < ${pkgname}-17.5.0-fix-import.patch
  cp -a pyopenssl-$pkgver{,-py2}
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/pyopenssl-$pkgver
  python setup.py build

  cd "$srcdir"/pyopenssl-$pkgver-py2
  python2 setup.py build
}

check() {
  (
    cd "$srcdir"/pyopenssl-$pkgver
    python setup.py pytest

    cd "$srcdir"/pyopenssl-$pkgver-py2
    python2 setup.py pytest
  ) || warning "Tests failed due to missing SSLv3_METHOD"
}

package_python-pyopenssl() {
  depends=('openssl' 'python' 'python-six' 'python-cryptography')
  replaces=('pyopenssl')
  provides=('pyopenssl')
  conflicts=('pyopenssl')

  cd pyopenssl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pyopenssl() {
  depends=('openssl' 'python2' 'python2-six' 'python2-cryptography')

  cd pyopenssl-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
