# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/service|/run/runit/service|g' -e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname=clamav-runit
pkgver=20180224
pkgrel=1
pkgdesc="runit service scripts for clamav"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
# Note: While this PKGBUILD is licensed under BSD-3 terms, all of the
#       included runscript should follow it's main package's licenses.
groups=('runit-world')
depends=('clamav')
install='clamav-runit.install'
provides=('clamav-runscripts')
replaces=('clamav-runscripts')
conflicts=('systemd-sysvcompat')
source=("clamd.run::${_url}/clamav/files/clamd/run")
sha256sums=('8ab29771e3000314c7e356190db0a44b19de1b6865389811f4b8d8548725255a')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'clamd'
}
