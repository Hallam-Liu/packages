# Maintainer: artoo <artoo@cromnix.org>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgbase=live-services
pkgname=('artix-live-base'
	'artix-live-openrc'
	'artix-live-portable-efi'
	'artix-live-runit')
pkgver=0.5
pkgrel=1
pkgdesc='Artix live session'
arch=('any')
url="https://github.com/artix-linux/live-services"
license=('GPL')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('21089cd13853ebff447d3e94e3d4bbe4cff7ae2fc1abcdebb1412fc0ae448753')

build() {
    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr SYSCONFDIR=/etc
}

package_artix-live-base() {
    pkgdesc='Artix live base scripts'
    depends=("artools-base>=${pkgver}")

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr SYSCONFDIR=/etc DESTDIR=${pkgdir} install_base
}

package_artix-live-openrc() {
    pkgdesc='Artix live openrc init scripts'
    depends=('openrc' 'artix-live-base')

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr SYSCONFDIR=/etc DESTDIR=${pkgdir} install_rc
}

package_artix-live-portable-efi() {
    pkgdesc='Artix live session portable efi settings'
    depends=('artix-grub-theme')

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr SYSCONFDIR=/etc DESTDIR=${pkgdir} install_portable_efi
}

package_artix-live-runit() {
    pkgdesc='Artix live runit init scripts'
    depends=('runit-artix' 'artix-live-base')

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr SYSCONFDIR=/etc DESTDIR=${pkgdir} install_runit
}
