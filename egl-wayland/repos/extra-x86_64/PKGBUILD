# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=egl-wayland
pkgver=0+23
pkgrel=2
pkgdesc="EGLStream-based Wayland external platform"
arch=(x86_64)
url=https://github.com/NVIDIA/egl-wayland
license=(MIT)
depends=(wayland eglexternalplatform)
makedepends=(meson git)
_commit=68ffe6fff49fff7667e8bab5b743c3e6c1950a6f  # master
source=("git+$url#commit=$_commit" unbreak-meson.diff
        10_nvidia_wayland.json)
sha256sums=('SKIP'
            'c9a1aa2ff24219c3177749d8c14d22ac49f964bf90dc1275d90c2e1a8b2f6cf1'
            '5cccf1905a266e8e34d5ad4aad4be85390e60b1a0850a29dd9d64adc641de412')

pkgver() {
  cd $pkgname
  printf '0+%s' "$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../unbreak-meson.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dt "$pkgdir/usr/share/egl/egl_external_platform.d" -m644 10_nvidia_wayland.json
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 et:
