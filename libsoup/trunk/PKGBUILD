# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup
pkgver=2.62.0
pkgrel=1
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libxml2 glib-networking sqlite krb5)
makedepends=(intltool gobject-introspection python vala git gtk-doc)
checkdepends=(apache php-apache)
_commit=0007d164002572cbd770dab8e0a36b00dba437eb  # tags/2.62.0^0
source=("git+https://git.gnome.org/browse/libsoup#commit=$_commit"
        0001-Configure-PHP-to-load-the-XMLRPC-extension-if-presen.patch
        0002-xmlrpc-server.php-Don-t-try-to-count-NULL-params.patch)
sha256sums=('SKIP'
            '7bf5257afb2c248c8ef8709e4aeaaafe9252835887b6944fe143eddc7ce98a8c'
            '9238d9a2deff3ba57fb8ebd03d70564f872f785798750af9a455b616b6e2190e')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugzilla.gnome.org/show_bug.cgi?id=782410
  patch -Np1 -i ../0001-Configure-PHP-to-load-the-XMLRPC-extension-if-presen.patch

  # tests fail with php 7.2
  patch -Np1 -i ../0002-xmlrpc-server.php-Don-t-try-to-count-NULL-params.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
