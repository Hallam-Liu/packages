# Maintainer: artoo <artoo@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

pkgname=opentmpfiles
pkgver=0.1.3
pkgrel=7
pkgdesc="A standalone utility for handling systemd-style tmpfiles.d settings"
arch=('any')
url="https://github.com/OpenRC/opentmpfiles"
license=('BSD2')
depends=('pacman')
backup=('etc/conf.d/opentmpfiles-dev'
        'etc/conf.d/opentmpfiles-setup')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        'opentmpfiles.hook'
        'tmpfiles-hook')
sha256sums=('1fdd4587c62d815296fb4162002cf001e3ed7aae8727d9b4360f527169e6b3be'
            '23c73b3cc20ec38e0a5166f254293c911d20ae3efec8e133f424c1a1d0703fb8'
            'adbee03530006e8284ea44c5dd7eb99f6cc6c4e383c09fec36d7a22f7d486fc5')

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make bindir="/usr/bin" DESTDIR="${pkgdir}" install

    for f in opentmpfiles-dev opentmpfiles-setup; do
        install -Dm755 openrc/$f.confd ${pkgdir}/etc/conf.d/$f
        sed -e 's|#!/sbin/runscript|#!/usr/bin/openrc-run|g' \
            -i ${pkgdir}/etc/conf.d/$f
        install -Dm755 openrc/$f.initd ${pkgdir}/etc/init.d/$f
    done

    install -dm755 ${pkgdir}/etc/runlevels/{boot,sysinit}
    ln -snf "/etc/init.d/opentmpfiles-dev" "${pkgdir}/etc/runlevels/sysinit/opentmpfiles-dev"
    ln -snf "/etc/init.d/opentmpfiles-setup" "${pkgdir}/etc/runlevels/boot/opentmpfiles-setup"

    # pacman hooks
    install -Dm755 ${srcdir}/tmpfiles-hook "$pkgdir"/usr/share/libalpm/scripts/tmpfiles-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook

    ln -snf "/usr/bin/tmpfiles" "${pkgdir}/usr/bin/systemd-tmpfiles"

    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 "${srcdir}/${pkgname}-${pkgver}/license" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
