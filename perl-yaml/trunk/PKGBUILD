# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-yaml
pkgver=1.25
pkgrel=1
pkgdesc="Perl/CPAN Module YAML : YAML Aint Markup Language"
arch=('any')
url="http://search.cpan.org/dist/YAML/"
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-test-deep' 'perl-test-yaml')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/T/TI/TINITA/YAML-${pkgver}.tar.gz)
sha512sums=('e7c04130aea19aa64f01be2879305a162a4a2241f495bdda92da63d66891ae752e5363d19fd8271fc759b43d0b0210120eb54c302b5496dd88eb423610161a1c')

build() {
  cd YAML-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-${pkgver}
  make test
}

package() {
  cd YAML-${pkgver}
  make DESTDIR="${pkgdir}" install
}
