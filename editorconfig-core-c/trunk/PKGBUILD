# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Matt Harrison <matt@harrison.us.com>
# Contributor: Erik van der Kolk <developer at smerik dot nl>
# Contributor: Hong Xu

pkgname=editorconfig-core-c
pkgver=0.12.2
pkgrel=1
pkgdesc="EditorConfig core code written in C (for use by plugins supporting EditorConfig parsing)"
arch=(x86_64)
url="https://github.com/editorconfig/editorconfig-core-c"
license=(BSD)
depends=(pcre)
makedepends=(cmake doxygen)
source=($pkgname-$pkgver.tar.gz::"https://github.com/editorconfig/${pkgname}/archive/v$pkgver.tar.gz")
sha256sums=('8a63ae63d3a2f2a202acbd94a6cbe3fa4450c2658e33e922683af33c48a9115d')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
