# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-wayland
_qtver=5.10.1
pkgver=${_qtver/-/}
pkgrel=3
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides APIs for Wayland'
depends=('qt5-declarative' 'libxcomposite' 'wayland') # namcap note: wayland is needed for nvidia-libgl users
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        qtwayland-key-compose.patch::"https://code.qt.io/cgit/qt/qtwayland.git/patch/?id=57c4af2b"
        qtbug-62044.patch
        qtbug-67150.patch::"https://code.qt.io/cgit/qt/qtwayland.git/patch/?id=26a6372b"
        qtbug-66867.patch::"https://code.qt.io/cgit/qt/qtwayland.git/patch/?id=e283cc05"
        qtwayland-context-create.patch::"https://code.qt.io/cgit/qt/qtwayland.git/patch/?id=7ce033cb")
sha256sums=('f5a7643a5ebcdc50d02b293191e675f387f67dc360c27bf6f94345372fba6356'
            'a96b50265791b0d6288deacde3bf75cd4aaf0429412f89190c787793d4a98901'
            '4b0ac091873b7a2b156ed2af4b2e32ecb22bfb99618f1ae76e9b72670933c3dd'
            '88aef190da6b99641bd9d039902d6d4b5a007b9920535694ee48884d9f7a554f'
            '223c5d752e0e0edc36492dc5e4e4ae5b65c61ec30ce499c044c2e99ac31c6e6f'
            '2717e699701232164447ce73c482f79cdcb7c11ea22cfee1aeb210f106d91c38')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -p1 -i ../qtwayland-key-compose.patch # Backport key composition support
  patch -p1 -i ../qtbug-62044.patch # Fix crash when connecting a new screen
# Backport some crash fixes
  patch -p1 -i ../qtbug-67150.patch
  patch -p1 -i ../qtbug-66867.patch
  patch -p1 -i ../qtwayland-context-create.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build

  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
