# Maintainer: Chris Cromer <chris@cromer.cl>

_pkgname=runit-artix
_servicedir=/run/runit/service

pkgname=runit-rc
pkgver=20180506
pkgrel=1
pkgdesc='Artix Linux runit initscripts'
arch=('x86_64')
url='https://github.com/artix-linux/runit-artix'
license=('BSD')
backup=('etc/rc/rc.local'
        'etc/rc/rc.shutdown'
        'etc/rc/rc.conf')
depends=('procps-ng' 'awk' 'bash' 'opentmpfiles' 'bootlogd')
provides=('init-kmod' 'init-udev' 'init-opentmpfiles' 'init-opensysusers')
conflicts=('init-kmod' 'init-udev' 'init-opentmpfiles' 'init-opensysusers')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('243bf99bea5783bd15d9703acc4987787c4e914482d8137abff138dafbb5b709')

build() {
	cd "${_pkgname}-${pkgver}"
	make SERVICEDIR="${_servicedir}"
}

package() {
	cd "${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" PREFIX="/usr" SERVICEDIR="${_servicedir}" install-rc
}
