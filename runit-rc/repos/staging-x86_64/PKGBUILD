# Maintainer: Chris Cromer <chris@cromer.cl>

_pkgname=runit-artix
_servicedir=/run/runit/service

pkgname=runit-rc
pkgver=20180509
pkgrel=1
pkgdesc='Artix Linux runit initscripts'
arch=('x86_64')
url='https://github.com/artix-linux/runit-artix'
license=('BSD')
backup=('etc/rc/rc.local'
        'etc/rc/rc.shutdown'
        'etc/rc/rc.conf')
depends=('procps-ng' 'awk' 'bash' 'opentmpfiles' 'bootlogd')
provides=('init-kmod' 'init-udev' 'init-opentmpfiles' 'init-opensysusers')
conflicts=('init-kmod' 'init-udev' 'init-opentmpfiles' 'init-opensysusers')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('d8a4419ef7f9860561ee196c9400f52ec5c07e1e2679ba3e6cc903d747b5fc95')
optdepends=('lvm2-runit: LVM support for runit'
            'cryptsetup-runit: Enable boot support for encrypted partitions')

build() {
	cd "${_pkgname}-${pkgver}"
	make SERVICEDIR="${_servicedir}"
}

package() {
	cd "${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" PREFIX="/usr" SERVICEDIR="${_servicedir}" install-rc
}
