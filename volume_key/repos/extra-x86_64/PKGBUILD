# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=volume_key
pkgver=0.3.10
pkgrel=1
pkgdesc="A library for manipulating storage volume encryption keys and storing them separately from volumes to handle forgotten passphrases"
arch=('x86_64')
url="https://pagure.io/volume_key"
license=('GPL')
depends=('cryptsetup' 'nss' 'gpgme')
makedepends=('python2' 'swig')
optdepends=('python2: for python2 bindings')
source=("https://github.com/felixonmars/volume_key/archive/volume_key-$pkgver.tar.gz")
sha512sums=('1c15bce7ccffc1da6edcb7a5643d84723a23e7fbee2ba4f700ef535ba1f4cdc3f8ea384dd86ea4916b7194f7ce86499a75e6157070b530118c8e0c7250782ef7')

prepare() {
  cd "$srcdir"/volume_key-volume_key-$pkgver
  
  autoreconf -i

  find . -name ".pyc" -delete
}

build() {
  cd "$srcdir"/volume_key-volume_key-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/volume_key-volume_key-$pkgver
  make DESTDIR="$pkgdir" install
  sed -i '/#include <config.h>/d' "$pkgdir"/usr/include/volume_key/libvolume_key.h
}
