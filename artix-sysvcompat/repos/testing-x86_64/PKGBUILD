# Maintainer: Muhammad Herdiansyah <koni@artixlinux.org>
# Maintainer: artoo <artoo@artixlinux.org>

_pkgname=halt

pkgname=artix-sysvcompat
pkgver=0.3.7
pkgrel=7
pkgdesc="Artix Linux's wrapper program for OpenRC and runit's init & shutdown scheme"
arch=('x86_64')
groups=('base')
url="https://github.com/artix-linux/halt"
depends=('svc-manager')
conflicts=('sysvinit' 'systemd-sysvcompat')
replaces=('sysvinit')
license=('BSD')
source=("$url/archive/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        'initswitch-hook'
        'initswitch.hook'
        'sysvcompat.hook')
sha256sums=('0b8ac1cfd75e76151ddbe2c0395ee8463c34b6e3c464922355cfb58818c98748'
            'babe3a338cb4876b9cfe15737b2a756e7da6da2a0f7b13420e68a798ee113bf8'
            'd96bc4b6723e1597281557a42936162c158ced63574d60856bfb387056a00f87'
            'ba9699ebbc72f9e93c062bfb3fe9f8213f8912ae66f1334f6ea5943115a72dd3')

build() {
	cd "${_pkgname}-${pkgver}"
	make
}

package() {
	cd "${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/halt/COPYING"

	# pacman hooks
    install -Dm755 ${srcdir}/initswitch-hook "${pkgdir}"/usr/share/libalpm/scripts/initswitch-hook
    install -Dm644 -t "${pkgdir}"/usr/share/libalpm/hooks ${srcdir}/*.hook

    # provide default symlink for PM file tracking
    install -d "${pkgdir}"/usr/bin
    ln -sf openrc-init "${pkgdir}"/usr/bin/init
}
