# Maintainer: Muhammad Herdiansyah <koni@artixlinux.org>
# Maintainer: artoo <artoo@artixlinux.org>

_pkgname=halt

pkgname=artix-sysvcompat
pkgver=0.3.7
pkgrel=1
pkgdesc="Artix Linux's wrapper program for OpenRC and runit's init & shutdown scheme"
arch=('x86_64')
groups=('base')
url="https://github.com/artix-linux/halt"
# provides=('init' 'halt')
depends=('svc-manager')
conflicts=('sysvinit' 'systemd-sysvcompat' 'halt')
replaces=('sysvinit' 'halt')
license=('BSD')
source=("$url/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
        'initswitch-hook'
        'initswitch.hook')
sha256sums=('0b8ac1cfd75e76151ddbe2c0395ee8463c34b6e3c464922355cfb58818c98748'
            '6ceb37e7d786ee879cfbb1fcdc9e7172e492914606f9369007e6c5cacba670c1'
            '3d103bf8130a0a9989d171282e40fbfda56db475014f62cc60cc8aab60495cd8')

build() {
	cd "$_pkgname-$pkgver"
	make
}

package() {
	cd "$_pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/halt/COPYING"

	# pacman hooks
    install -Dm755 ${srcdir}/initswitch-hook "$pkgdir"/usr/share/libalpm/scripts/initswitch-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook
}
