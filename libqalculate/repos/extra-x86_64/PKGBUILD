# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libqalculate
pkgver=2.4.0
pkgrel=1
pkgdesc="Multi-purpose desktop calculator"
arch=(x86_64)
url="http://qalculate.github.io/"
license=(GPL)
depends=(libxml2 curl mpfr)
makedepends=(intltool)
optdepends=('gnuplot: for plotting support')
source=(https://github.com/Qalculate/libqalculate/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8a0a7176c9d9cf3fcf0c9fcd3d4d4f70c9c09248b41017b0a77e4fb932e6ff5c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
